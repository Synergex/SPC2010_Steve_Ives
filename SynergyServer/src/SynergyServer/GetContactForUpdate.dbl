
{xfMethod(interface="SynergyServer",elb="EXE:SynergyServer")}

;;;<summary>Returns a contact record and current GRFA</summary>
;;;<returns>Return value defined by METHOD_STATUS enumeration</returns>
;;;<param name="CustomerID">Passed customer ID</param>
;;;<param name="ContactID">Passed contact ID</param>
;;;<param name="Contact">Returned contact structure</param>
;;;<param name="GRFA">Returned current GRFA</param>
;;;<param name="Message">Returned message text for non success status</param>

function GetContactForUpdate, METHOD_STATUS

	{xfParameter(name="CustomerID")}
	required in a_customer_id, int

	{xfParameter(name="ContactID")}
	required in a_contact_id, int

	{xfParameter(name="Contact")}
	required out a_contact, strContact

	{xfParameter(name="GRFA",type=SynType.binaryhandle)}
	required out a_grfa, i4
	
	{xfParameter(name="Message")}
	required out a_message, string

	endparams

	stack record local_data
		ch		,int
		sts		,METHOD_STATUS
	endrecord

proc
	
	init local_data, a_contact

	a_contact.customer_id = a_customer_id
	a_contact.contact_id = a_contact_id

	a_grfa = mem_proc(DM_RESIZ,10,a_grfa)

	try
	begin
		open(ch=syn_freechn(),i:i,"DAT:contact.ism")
		read(ch,a_contact,keyval(ch,a_contact,0),KEYNUM:0,GETRFA:^m(a_grfa))
		sts = METHOD_STATUS.SUCCESS
		a_message = ""
	end
	catch (ex, @KeyNotSameException)
	begin
		clear a_contact, ^m(a_grfa)
		sts = METHOD_STATUS.INFO_ERROR
		a_message = "Contact not found!"
	end
	catch (ex, @EndOfFileException)
	begin
		clear a_contact, ^m(a_grfa)
		sts = METHOD_STATUS.INFO_ERROR
		a_message = "Contact not found!"
	end
	catch (ex)
	begin
		clear a_contact, ^m(a_grfa)
		sts = METHOD_STATUS.FATAL_ERROR
		a_message = "A fatal error occurred!"
	end
	finally
	begin
		if (ch&&chopen(ch))
			close ch		
	end
	endtry

	freturn sts
	
endfunction

